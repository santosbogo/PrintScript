plugins {
    id "org.jlleitschuh.gradle.ktlint"
    id 'org.jetbrains.kotlinx.kover'
    id 'io.gitlab.arturbosch.detekt'
}

repositories {
    mavenCentral()
}

subprojects {
    apply plugin: 'org.jetbrains.kotlinx.kover'
}

dependencies {
    kover(project(":shared"))
    kover(project(":lexer"))
    kover(project(":parser"))
    kover(project(":interpreter"))
    kover(project(":linter"))
    kover(project(":formatter"))
    kover(project(":cli"))
}

kover {
    useJacoco()
    reports {
        verify {
            rule {
                minBound(80)
            }
        }
    }
}

tasks.register('build') {
    dependsOn ":koverVerify", ":koverHtmlReport", "ktlintCheck"
}

tasks.register('check') {
    dependsOn ":koverVerify", "ktlintCheck", "test"
}

detekt {
    toolVersion = "1.23.5"
    config.setFrom(file("${rootDir}/config/detekt/detekt.yml"))
    buildUponDefaultConfig = true
}

tasks.register("reportMerge", io.gitlab.arturbosch.detekt.report.ReportMergeTask) {
    output = project.layout.buildDirectory.file("reports/detekt/merge.xml") // or "reports/detekt/merge.sarif"
}

subprojects {
    detekt {
        reports.xml.required.set(true)
        // reports.sarif.required.set(true)
    }

    tasks.withType(io.gitlab.arturbosch.detekt.Detekt).configureEach {
        finalizedBy(reportMerge)
    }

    reportMerge.configure {
        input.from(tasks.withType(io.gitlab.arturbosch.detekt.Detekt).collect { it.xmlReportFile }) // or sarifReportFile
    }
}
